neuron iaf_psc_exp_nonlineardendrite:

  initial_values:
    V_m mV = 0mV      # Membrane potential
  end

  equations:
    kernel I_kernel1 = exp(-1/tau_syn1*t)
    kernel I_kernel2 = (e/tau_syn2) * t * exp(-t/tau_syn2)
    kernel I_kernel3 = exp(-1/tau_syn3*t)

    recordable inline I_dend pA = convolve(I_kernel2, I_2)
 
    inline I_syn pA = convolve(I_kernel1, I_1) + I_dend + convolve(I_kernel3, I_3) + I_e

    V_m' = -(V_m-E_L)/tau_m + I_syn/C_m 
  end

  parameters:
    C_m pF = 250 pF          # capacity of the membrane
    tau_m ms = 20 ms         # membrane time constant
    tau_syn1 ms = 10 ms      # time constant of synaptic current, port 1
    tau_syn2 ms = 10 ms      # time constant of synaptic current, port 2
    tau_syn3 ms = 10 ms      # time constant of synaptic current, port 3
    V_th mV = 25 mV          # spike threshold
    V_reset mV = 0 mV        # reset voltage
    I_e    pA = 0pA          # External current
    E_L    mV = 0mV          # Resting potential

    # dendritic action potential     
    i_th pA = 60pA           # current-threshold for a dendritic spike
  end

  input:
    I_1 pA <- spike
    I_2 pA <- spike
    I_3 pA <- spike
  end

  output: spike

  update:
    # solve ODEs
    integrate_odes()

    # current-threshold, emit a dendritic spike 
    if I_dend > i_th:
      I_dend = 0 pA
      I_dend' = 0 pA/ms
    end 

    # emit somatic spike
    if V_m > V_th:
      emit_spike()
      V_m = V_reset
    end
  end
end

